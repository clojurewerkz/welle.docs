---
title: "Welle, a Riak Clojure client: Using Riak secondary indexes"
layout: article
---

## About this guide

This guide covers:

 * What are secondary indexes (2i)
 * Riak's approach to secondary indexes
 * Indexing stored values with Welle
 * Secondary index queries

This work is licensed under a <a rel="license" href="http://creativecommons.org/licenses/by/3.0/">Creative Commons Attribution 3.0 Unported License</a> (including images & stylesheets). The source is available [on Github](https://github.com/clojurewerkz/welle.docs).


## What version of Welle does this guide cover?

This guide covers Welle 1.5.


## What version of Riak does this guide cover?

[Secondary indexes in Riak](http://wiki.basho.com/Secondary-Indexes.html) are supported starting with Riak 1.0 and only by buckets
that use [LevelDB storage backend](http://wiki.basho.com/LevelDB.html).


## Introduction

According to Wikipedia, a [database index](http://en.wikipedia.org/wiki/Database_index) is a data structure that improves the speed of data retrieval operations.
*Secondary indexes* are indexes on keys other then the primary key (the one you specify when storing a value in Riak). For example, if an application has
accounts that look like this:

``` clojure
{:first-name "John"
 :last-name "Doe"
 :username "johndoe"
 :email "jdoe@example.com"
 :password-hash "…"
 :password-salt "…"}
```

then it may be desirable to be able to find an account with the given email so that it can be authenticated. One approach would be to use email
as primary key but it is not always possible or practical. Secondary indexes is what makes it possible to *efficiently* retrieve an account
with a particular email.


## Secondary Indexes in Riak

Many databases (for example, PostgreSQL or Oracle) require indexes to be created before you can execute queries over them. In addition, they
(typically) create index entries for every inserted row (this behavior can be made conditional in PostgreSQL). This works well when schema is
fixed and known upfront but this is not the case with Riak. Riak treats stored values as "bags of bytes" and they can have any shape.

Because of that, secondary indexes work differently with Riak. With Riak, index entries (field/value pairs) are specified when a value is stored.
Then, every time the value is updated, index entries may change and reindexing will happen. In other words, secondary indexes are flexible, just
like the schema.

Riak supports two kinds of 2i queries: value (also known as "equality") and range. The email example falls into the former category, while
timestamp-based queries like "all events between 3 days ago and today" fall into the latter.


## Indexing Stored Values

To specify index entries on a stored value, use the `:indexes` option of `clojurewerkz.welle.kv/store` function:

``` clojure
(ns welle.docs.examples
  (:require [clojurewerkz.welle.core    :as wc]
            [clojurewerkz.welle.buckets :as wb]
            [clojurewerkz.welle.kv      :as kv])
  (:import com.basho.riak.client.http.util.Constants))

(wc/connect!)
(wb/create "accounts")

(let [key   "novemberain"
      email "michael@example.com"
      val   {:name "Michael" :age 27 :username key :email email}]
  (kv/store "accounts" key val :content-type Constants/CTYPE_JSON_UTF8 :indexes {:email #{email}}))
```

`:indexes` value must be a map where keys are names of indexed fields (that can be keywords or strings) and values are **sets** of values.
It is common to have just one value in a set like in the example above, but there also may be more than one:

``` clojure
(ns welle.docs.examples
  (:require [clojurewerkz.welle.core    :as wc]
            [clojurewerkz.welle.buckets :as wb]
            [clojurewerkz.welle.kv      :as kv]))

(wc/connect!)
(wb/create "accounts")

(let [key       "novemberain"
      languages #{"clojure" "java" "ruby" "scala" "erlang"}
      val       {:name "Michael" :age 27 :username key :created-at (java.util.Date.) :hacks languages}]
  (kv/store "accounts" key val :content-type "application/clojure" :indexes {:language languages}))
```

If you need to index date/time values, use timestamps (longs):

``` clojure
(kv/store "accounts" key val :content-type Constants/CTYPE_JSON_UTF8 :indexes {:email #{email} :created-at #{(to-timestamp (now))}})
```

For anything that involves working with date, time or timestamp values, it is highly recommended that you use [clj-time](https://github.com/seancorfield/clj-time) and its convenient
[coercion functions](https://github.com/seancorfield/clj-time/blob/master/src/clj_time/coerce.clj)


## Secondary Index Queries

As it was mentioned before, there are two types of 2i queries in Riak: value (equality) and range queries. Both are performed using the same function,
`clojurewerkz.welle.kv/index-query` that takes a bucket name, a field to query against and a field value. Here is a value query example:

``` clojure
(ns welle.docs.examples
  (:require [clojurewerkz.welle.core    :as wc]
            [clojurewerkz.welle.buckets :as wb]
            [clojurewerkz.welle.kv      :as kv])
  (:import com.basho.riak.client.http.util.Constants))

(wc/connect!)
(wb/create "accounts")

(let [key   "novemberain"
      email "michael@example.com"
      val   {:name "Michael" :age 27 :username key :email email}]
  ;; store an object
  (kv/store "accounts" key val :content-type Constants/CTYPE_JSON_UTF8 :indexes {:email #{email}})
  ;; 2i query
  (kv/index-query "accounts" :email email))
```

To perform a range query, pass a vector of two range boundaries as the field value:

``` clojure
;; 2i range query
(kv/index-query "accounts" :age [23 30])
```

`clojurewerkz.welle.kv/index-query` returns **a collection of keys** as its result. You then use those keys to fetch associated objects.


## Wrapping Up

Secondary indexes in Riak are à la carte, just like schema of stored values. They greatly enhance Riak's querying capabilities. Using them
with Welle is straightforward, just like it should be.


## What to Read Next

The documentation is organized as a number of guides, covering all kinds of topics.

We recommend that you read the following guides first, if possible, in this order:

 * [Links and link walking](/articles/links.html)
 * [Map/Reduce](/articles/mapreduce.html)
 * [Integration with 3rd party libraries](/articles/integration.html)



## Tell Us What You Think!

Please take a moment to tell us what you think about this guide on Twitter or the [Welle mailing list](https://groups.google.com/forum/#!forum/clojure-riak)

Let us know what was unclear or what has not been covered. Maybe you do not like the guide style or grammar or discover spelling mistakes. Reader feedback is key to making the documentation better.
